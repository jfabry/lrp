;; A line follower for a differential drive robot
;; Uses the Lego color sensor,
;; position is front and center, and pointed to the line (down)
(var sensor := [robot sensor3])
(var mright := [robot motorA])
(var mleft := [robot motorD])
(var speed := [5])
(machine follower
	(state init
		(onentry [sensor setMode: #Mode2])
	)
	(state moving
		(onentry [
			mright startAtSpeed: speed.
			mleft startAtSpeed: speed.
			])
		(onexit [robot fullStop])
	)
	(state looking
		(machine lookalgo
			(var looktime := [2000])
			(state lookright
				(onentry 
					[mright startAtSpeed: speed negated.
					mleft startAtSpeed: speed.])
				(onexit [looktime := looktime*2]))
			(state lookleft
				(onentry 
					[mright startAtSpeed: speed.
					mleft startAtSpeed: speed negated.])
				(onexit [looktime := looktime * 2]))
			(ontime looktime lookright -> lookleft tlr)
			(ontime looktime lookleft -> lookright tfail)
			)
		(onentry (spawn lookalgo lookright))
		)
	(eps init -> moving tinit)
	(on out moving -> looking tms)
	(event out [(sensor read = 1) not])
	(event in  [sensor read = 1])
	(on in looking -> moving tsm)
)
(spawn follower init)