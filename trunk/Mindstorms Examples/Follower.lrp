;; A line follower for a differential drive robot
;; Uses the Lego color sensor,
;; position is front and center, and pointed to the line (down)
(var sensor := [robot sensor3])
(var mright := [robot motorA])
(var mleft := [robot motorD])
(var speed := [4])
(machine follower
	(state init
		(onentry [sensor setMode: #Mode2])
	)
	(state moving
		(onentry [
			mright startAtSpeed: speed * 2.
			mleft startAtSpeed: speed * 2.
			])
		(onexit [robot fullStop])
	)
	(state looking
		(machine lookalgo
			(var looktime := [1000])
			(state lookright
				(onentry 
					[mright startAtSpeed: speed negated.
					mleft startAtSpeed: speed.]))
			(state lookleft
				)
			(state centerfromright
				(onentry 
					[mright startAtSpeed: speed.
					mleft startAtSpeed: speed negated.]))
			(state centerfromleft
				(onentry 
					[mright startAtSpeed: speed negated.
					mleft startAtSpeed: speed.])
				(onexit [looktime := looktime * 2]))
			(ontime looktime lookright -> centerfromright tlrb)
			(ontime looktime centerfromright -> lookleft tlr)
			(ontime looktime lookleft -> centerfromleft tfail)
			(ontime looktime centerfromleft -> lookright tfailb)
			)
		(onentry (spawn lookalgo lookright))
		)
	(eps init -> moving tinit)
	(on out moving -> looking tms)
	(event out [(sensor read = 1) not])
	(event in  [sensor read = 1])
	(on in looking -> moving tsm)
)
(spawn follower init)
