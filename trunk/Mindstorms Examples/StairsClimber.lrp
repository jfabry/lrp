;;; A stairs climber building using the MindStorm expansion set
;;; Use the new JetStorm Bridge funcionality
;;; Use ontime transition to estimate the second step of the climbing
;;; Watch example in: http://youtu.be/6HXcKwMO8Fo

(var backWheelsSpeed := [-50])
(var frontWheelsSpeed := [-50])
(var climbSpeed := [-50])
(var startGyro := [0])
(var deltaGyro := [4])
(machine Stair
  (state forward
    (onentry [
      robot motorA startAtSpeed: backWheelsSpeed.
      robot motorB startAtSpeed: frontWheelsSpeed.
      startGyro := robot sensor2 read.
      ]
    )
    (onexit [
      robot fullStop.
      ]
    )
  )
  
  (state climb
    (onentry [
      robot motorD startAtSpeed: climbSpeed.
      robot motorB startAtSpeed: frontWheelsSpeed.
      ]
    )
    (onexit [
      robot fullStop.
      ]
    )
  )
  
  (state forward2
    (onentry [
      robot motorA startAtSpeed: backWheelsSpeed.
      robot motorB startAtSpeed: frontWheelsSpeed.
      ]
    )
  )
  
  (state climb2
    (onentry [
      robot motorD startAtSpeed: climbSpeed negated.
      ]
    )
    (onexit [
      robot fullStop 
      ]
    )
  )
  
  (state stop
    (onentry [robot fullStop])
  )
  
  (on incline forward -> climb incline)
  (on finishClimb climb -> forward2 finishClimb)
  (event finishClimb [robot sensor3 read == 1])
  (event incline [robot sensor2 read > (deltaGyro + startGyro)])
  
  (ontime 3000 forward2 -> climb2 for2)
  (ontime 6000 climb2 -> forward loop)
)
(spawn Stair forward)