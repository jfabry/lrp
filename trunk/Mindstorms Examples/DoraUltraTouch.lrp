;;; New program created at 17 April 2015
;; A Random Space Explorer behavior for a differential drive robot
;; Wall collisions are detected with the ultrasonic sensor and touch sensors

(var speed :=[20])
(var timeout := [1000 atRandom])
(machine Dora
  (state forward
    (onentry [
      robot motorA startAtSpeed: speed.
      robot motorD startAtSpeed: speed.
    ])
    (onexit [robot fullStop]))
  (state shock
    (onentry [
      robot motorA startAtSpeed: speed negated.
      robot motorD startAtSpeed: speed negated.
    ])
    (onexit [robot fullStop]))
  (state turn
    (onentry [
      robot motorA startAtSpeed: speed.
      robot motorD startAtSpeed: speed negated
    ])
    (onexit [
      robot fullStop.
      timeout := 1000 atRandom
    ]))
  (ontime 500 shock -> turn st)
  (ontime timeout turn -> forward tf)
  (on toclose forward -> shock fs)
  (on shockLeft forward -> shock sl)
  (on shockRight forward -> shock sf)
  (event toclose [robot sensor2 read < 200])
  (event shockLeft [robot sensor4 read = 1])
  (event shockRight [robot sensor1 read = 1])
)
(spawn Dora forward)